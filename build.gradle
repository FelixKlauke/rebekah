plugins {

    id 'java'
}

group 'de.d3adspace.rebekah'
version '1.0-SNAPSHOT'

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    repositories {

        /**
         * Resolving local maven repository.
         */
        mavenLocal()

        /**
         * Maven central repository.
         */
        mavenCentral()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) { publication ->
                from components.java
            }
        }
        repositories {
            maven {
                credentials {
                    username = System.getenv('NEXUS_USERNAME')
                    password = System.getenv('NEXUS_PASSWORD')
                }

                def releasesRepoUrl = 'https://repository.klauke-enterprises.com/repository/maven-releases/'
                def snapshotsRepoUrl = 'https://repository.klauke-enterprises.com/repository/maven-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }

    dependencies {
        /**
         * Google Guice for dependency injection.
         */
        compile group: 'com.google.inject', name: 'guice', version: '4.2.0'

        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.4.2'
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.4.2'
        testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: '2.28.2'
    }

    test {
        useJUnitPlatform()
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled = true
        xml.setDestination(new File("${buildDir}/reports/jacoco/report.xml"))
        html.setEnabled(true)
        html.setDestination(new File("${buildDir}/reports/jacoco/report.html"))
    }

    dependencies {
        subprojects {
            test
        }
    }
}
